# Maintainer: nyfair <nyfair2012@gmail.com>

pkgname=ffmpeg-custom
pkgver=4.0.1
pkgrel=1
pkgdesc="Complete and free Internet live audio and video solution"
_zlibver=1.2.11
_lamever=3.100
_oggver=1.3.3
_vorbisver=1.3.6
_opusver=1.3-rc
arch=('x86_64')
url="http://ffmpeg.org"
provides=('ffmpeg')
makedepends=('yasm')
license=('LGPL')
source=("https://zlib.net/current/zlib-$_zlibver.tar.gz"
        "http://downloads.sourceforge.net/lame/lame-$_lamever.tar.gz"
        "http://downloads.xiph.org/releases/ogg/libogg-$_oggver.tar.xz"
        "http://downloads.xiph.org/releases/opus/opus-$_opusver.tar.gz"
        "https://github.com/AO-Yumi/vorbis_aotuv/archive/beta6.03-2018.tar.gz"
        "http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  TMP="${srcdir}/tmp"
  mkdir -p ${TMP}
  FLAGS="-march=core2 -O3 -pipe -ffat-lto-objects"
  export CFLAGS=${FLAGS}
  export CXXFLAGS=${FLAGS}
  export PKG_CONFIG_PATH=${TMP}/lib/pkgconfig

  cd "${srcdir}/zlib-$_zlibver/"
  CC=gcc ./configure --prefix=${TMP} --static
  make install

  HOST=x86_64-w64-mingw32
  CHOST="--build=$HOST --host=$HOST --target=$HOST"

  cd "${srcdir}/lame-$_lamever/"
  ./configure ${CHOST} prefix=${TMP} --disable-shared --disable-frontend
  CFLAGS="-msse2 ${CFLAGS}" make install

  cd "${srcdir}/libogg-$_oggver/"
  ./configure ${CHOST} prefix=${TMP} --disable-shared
  make install

  cd "${srcdir}/vorbis_aotuv-beta6.03-2018"
  ./configure ${CHOST} prefix=${TMP} --disable-shared --disable-docs --disable-examples
  CFLAGS="-I${TMP}/include ${CFLAGS}" LDFLAGS="-L${TMP}/lib ${LDFLAGS}" make install

  cd "${srcdir}/opus-$_opusver/"
  ./configure ${CHOST} prefix=${TMP} --disable-shared
  make install

  unset CFLAGS
  cd "${srcdir}/ffmpeg-$pkgver/"
  ./configure prefix=/usr --extra-cflags="-I${TMP}/include -I${TMP}/include/opus ${FLAGS}" \
    --extra-ldflags="-L${TMP}/lib ${LDFLAGS}" --target-os=mingw32 --arch=x86_64 \
    --disable-debug --disable-stripping --disable-shared --disable-doc \
    --enable-gpl --enable-version3 --enable-nonfree \
    --disable-ffplay --disable-ffprobe --disable-avdevice \
    --enable-libmp3lame --enable-libvorbis --enable-libopus \
    --disable-decoder=libvorbis --disable-decoder=libopus
    make
}

package() {
  cd "${srcdir}/ffmpeg-$pkgver/"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/bin/*.lib ${pkgdir}/usr/share ${pkgdir}/usr/lib ${pkgdir}/usr/include
}