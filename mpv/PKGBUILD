# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=mpv
pkgver=0.37.0
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player'
arch=('x86_64')
url='https://mpv.io/'
makedepends=('yasm' 'vapoursynth-dev' 'ffmpeg-dev' 'luajit-dev' 'ffnvcodec-dev' 'freetype2-dev' 'fribidi-dev' 'harfbuzz-dev' 'libass-dev' 'lcms2-dev'
              'libbluray-dev' 'libdovi-dev' 'libdvdcss-dev' 'libdvdread-dev' 'libdvdnav-dev' 'shaderc-dev' 'spirv-cross-dev' 'vulkan-dev' 'libplacebo-dev')
license=('GPL3')
source=("https://github.com/mpv-player/mpv/archive/v$pkgver.tar.gz")
md5sums=('SKIP')

build() {
  cd $srcdir/mpv-$pkgver
  sed -i 's/ .. exesuf//g'  player/lua/ytdl_hook.lua
  sed -i '/exesuf/d'  player/lua/ytdl_hook.lua
  export LDFLAGS="$LDFLAGS -static -lstdc++ -lssp"
  meson setup build --buildtype=release -Dprefix=/opt -Ddvdnav=enabled 
  meson compile -C build
}

package() {
  depends=('ffmpeg' 'luajit' 'vapoursynth' 'mcfgthread')
  export PKGEXT='.pkg.tar.xz'
  mkdir -p $pkgdir/opt/bin
  cd $srcdir/mpv-$pkgver/build
  mv mpv.exe $pkgdir/opt/bin
  strip -s $pkgdir/opt/bin/*
}
